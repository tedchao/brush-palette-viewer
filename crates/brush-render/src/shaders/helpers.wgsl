enable f16;

const TILE_WIDTH: u32 = 16u;
const TILE_SIZE: u32 = TILE_WIDTH * TILE_WIDTH;

// Compute linear workgroup ID from 2D dispatch
fn get_workgroup_id(wid: vec3u, num_wgs: vec3u) -> u32 {
    return wid.x + wid.y * num_wgs.x;
}

// Compute linear global invocation ID from 2D dispatch
fn get_global_id(wid: vec3u, num_wgs: vec3u, lid: u32, wg_size: u32) -> u32 {
    return get_workgroup_id(wid, num_wgs) * wg_size + lid;
}

// Helper function to compact bits for 2D z-order decoding
fn compact_bits_16(v: u32) -> u32 {
    var x = v & 0x55555555u;
    x = (x | (x >> 1u)) & 0x33333333u;
    x = (x | (x >> 2u)) & 0x0F0F0F0Fu;
    x = (x | (x >> 4u)) & 0x00FF00FFu;
    x = (x | (x >> 8u)) & 0x0000FFFFu;
    return x;
}

// Decode z-order to 2D coordinates
fn decode_morton_2d(morton: u32) -> vec2<u32> {
    let x = compact_bits_16(morton);
    let y = compact_bits_16(morton >> 1u);
    return vec2<u32>(x, y);
}

fn map_1d_to_2d(id: u32, tiles_per_row: u32) -> vec2<u32> {
    let tile_id = id / TILE_SIZE;
    let within_tile_id = id % TILE_SIZE;

    let tile_x = tile_id % tiles_per_row;
    let tile_y = tile_id / tiles_per_row;

    return vec2u(tile_x * TILE_WIDTH, tile_y * TILE_WIDTH) + decode_morton_2d(within_tile_id);
}

// Uniforms for projection passes.
struct ProjectUniforms {
    // View matrix transform world to view position.
    viewmat: mat4x4f,

    // Focal of camera (fx, fy)
    focal: vec2f,
    // Img resolution (w, h)
    img_size: vec2u,

    tile_bounds: vec2u,
    // Camera center (cx, cy).
    pixel_center: vec2f,

    // Position of camera (xyz + pad)
    camera_position: vec4f,

    // Degree of sh coefficients used.
    sh_degree: u32,

    total_splats: u32,
    num_visible: u32,

    pad_a: u32,
}

// Uniforms for rasterize pass.
struct RasterizeUniforms {
    tile_bounds: vec2u,
    img_size: vec2u,
    // Nb: Alpha is ignored atm.
    background: vec4f,
}

struct ProjectedSplat {
    xy_x: f32,
    xy_y: f32,
    conic_x: f32,
    conic_y: f32,
    conic_z: f32,

    color_a: f32,

    color_r: f16,
    color_g: f16,
    color_b: f16,

    _pad: f16,
}

fn create_projected_splat(xy: vec2f, conic: vec3f, color: vec4f) -> ProjectedSplat {
    return ProjectedSplat(xy.x, xy.y, conic.x, conic.y, conic.z, color.a, f16(color.r), f16(color.g), f16(color.b), f16(0.0));
}

struct PackedVec3 {
    x: f32,
    y: f32,
    z: f32,
}

fn get_bbox(center: vec2f, dims: vec2f, bounds: vec2u) -> vec4u {
    // get bounding box with center and dims, within bounds
    // bounding box coords returned in tile coords, inclusive min, exclusive max
    // clamp between 0 and tile bounds
    let min = vec2u(clamp(center - dims, vec2f(0.0), vec2f(bounds)));
    let max = vec2u(clamp(center + dims + vec2f(1.0), vec2f(0.0), vec2f(bounds)));
    return vec4u(min, max);
}

fn get_tile_bbox(pix_center: vec2f, pix_extent: vec2f, tile_bounds: vec2u) -> vec4u {
    // gets gaussian dimensions in tile space, i.e. the span of a gaussian in
    // tile_grid (image divided into tiles)
    let tile_center = pix_center / f32(TILE_WIDTH);
    let tile_extent = pix_extent / f32(TILE_WIDTH);
    return get_bbox(tile_center, tile_extent, tile_bounds);
}

fn sigmoid(x: f32) -> f32 {
    return 1.0f / (1.0f + exp(-x));
}

// device helper to get 3D covariance from scale and quat parameters
fn quat_to_mat(quat: vec4f) -> mat3x3f {
    // quat to rotation matrix
    let w = quat.x;
    let x = quat.y;
    let y = quat.z;
    let z = quat.w;

    let x2 = x * x;
    let y2 = y * y;
    let z2 = z * z;
    let xy = x * y;
    let xz = x * z;
    let yz = y * z;
    let wx = w * x;
    let wy = w * y;
    let wz = w * z;

    // See https://www.songho.ca/opengl/gl_quaternion.html
    return mat3x3f(
        vec3f(
            (1.0 - 2.0 * (y2 + z2)),
            (2.0 * (xy + wz)),
            (2.0 * (xz - wy)), // 1st col
        ),
        vec3f(
            (2.0 * (xy - wz)),
            (1.0 - 2.0 * (x2 + z2)),
            (2.0 * (yz + wx)), // 2nd col
        ),
        vec3f(
            (2.0 * (xz + wy)),
            (2.0 * (yz - wx)),
            (1.0 - 2.0 * (x2 + y2)) // 3rd col
        ),
    );
}

fn scale_to_mat(scale: vec3f) -> mat3x3f {
    return mat3x3(
        vec3f(scale.x, 0.0, 0.0),
        vec3f(0.0, scale.y, 0.0),
        vec3f(0.0, 0.0, scale.z)
    );
}

fn calc_cam_J(mean_c: vec3f, focal: vec2f, img_size: vec2u, pixel_center: vec2f) -> mat3x2f {
    let lims_pos = (1.15f * vec2f(img_size.xy) - pixel_center) / focal;
    let lims_neg = (-0.15f * vec2f(img_size.xy) - pixel_center) / focal;
    let rz = 1.0 / mean_c.z;

    // Get normalized image coords +- clipped to the frustum.
    let uv_clipped = clamp(mean_c.xy * rz, lims_neg, lims_pos);

    let duv_dxy = focal * rz;
    let J = mat3x2f(
        vec2f(duv_dxy.x, 0.0),
        vec2f(0.0, duv_dxy.y),
        -duv_dxy * uv_clipped
    );

    return J;
}

// Bit-level NaN/Inf test. `x == x` is the textbook check but compilers may
// fold it to `true` under fast-math; `bitcast` preserves the bit pattern
// and IEEE-754 Inf/NaN have exponent is 0xFF.
fn is_finite_f32(x: f32) -> bool {
    return ((bitcast<u32>(x) >> 23u) & 0xFFu) != 0xFFu;
}

fn is_finite_cov2d(c: mat2x2f) -> bool {
    return is_finite_f32(c[0][0]) && is_finite_f32(c[1][1]) && is_finite_f32(c[0][1]);
}

// 2D covariance from scale, quat and view params. For finite inputs whose
// V*V^T doesn't overflow, returns cov2d unchanged. For huge-but-finite
// products (log_scale ≈ 30 → cov2d ≈ 1e28 is a real training state),
// uniform-scales the whole matrix so max |entry| ≤ 1e18 — keeps det inside
// f32's limit, preserves PSD and the off-diagonal/diagonal ratio, and
// visually still reads as "covers the frame".
fn calc_cov2d(scale: vec3f, quat: vec4f, mean_c: vec3f, focal: vec2f, img_size: vec2u, pixel_center: vec2f, viewmat: mat4x4f) -> mat2x2f {
    let R_obj = quat_to_mat(quat);
    let R_cam = mat3x3f(viewmat[0].xyz, viewmat[1].xyz, viewmat[2].xyz);
    let J = calc_cam_J(mean_c, focal, img_size, pixel_center);

    // V = J * R_cam * R_obj * diag(scale)
    let N = R_cam * R_obj;
    let N_s = mat3x3f(N[0] * scale.x, N[1] * scale.y, N[2] * scale.z);
    let V = J * N_s;
    let raw = V * transpose(V);

    let lim = 1e18f;
    let max_abs = max(max(abs(raw[0][0]), abs(raw[1][1])), abs(raw[0][1]));
    let scale_down = select(1.0, lim / max_abs, max_abs > lim);
    return raw * scale_down;
}

#ifdef MIP_SPLATTING
    const COV_BLUR: f32 = 0.1;
#else
    const COV_BLUR: f32 = 0.3;
#endif

// 2x2 det with named intermediates so the compiler can't fuse the two
// product terms into an FMA. FMA vs separate multiply has ~1 ULP of wiggle.
// if PF and PV pick different choices, their MIP `filter_comp` factors
// differ, and the derived `power_threshold` desync breaks the PF/MG tile-hit
// agreement near contribution boundaries.
fn det2_strict(m: mat2x2f) -> f32 {
    let ad = m[0][0] * m[1][1];
    let bc = m[0][1] * m[1][0];
    return ad - bc;
}

fn compensate_cov2d(cov2d: ptr<function, mat2x2f>) -> f32 {
    let cov_start = *cov2d;
    var cov_end = *cov2d;
    // add a constant blur along axes.
    cov_end[0][0] += COV_BLUR;
    cov_end[1][1] += COV_BLUR;

#ifdef MIP_SPLATTING
    // Filter with isotropic gaussian and compute the compensation factor.
    // Uses `det2_strict` rather than `determinant(...)` so project_forward
    // and project_visible produce bit-identical MIP filter factors.
    let det_raw = max(det2_strict(cov_start), 0.0f);
    let det_blurred = det2_strict(cov_end);
    let filter_comp = sqrt(det_raw / det_blurred);
#else
    let filter_comp = 1.0f;
#endif
    *cov2d = cov_end;
    return filter_comp;
}

fn inverse(m: mat2x2f) -> mat2x2f {
    let det = determinant(m);
    if (det <= 0.0f) {
        return mat2x2f(vec2f(0.0), vec2f(0.0));
    }
    let inv_det = 1.0f / det;
    return mat2x2f(vec2f(m[1][1] * inv_det, -m[0][1] * inv_det), vec2f(-m[0][1] * inv_det, m[0][0] * inv_det));
}

fn cov_compensation(cov2d: vec3f) -> f32 {
    let cov_orig = cov2d - vec3f(COV_BLUR, 0.0, COV_BLUR);
    let det_orig = cov_orig.x * cov_orig.z - cov_orig.y * cov_orig.y;
    let det = cov2d.x * cov2d.z - cov2d.y * cov2d.y;
    return sqrt(max(0.0, det_orig / det));
}

fn calc_sigma(pixel_coord: vec2f, conic: vec3f, xy: vec2f) -> f32 {
    let delta = pixel_coord - xy;
    return 0.5f * (conic.x * delta.x * delta.x + conic.z * delta.y * delta.y) + conic.y * delta.x * delta.y;
}

fn calc_vis(pixel_coord: vec2f, conic: vec3f, xy: vec2f) -> f32 {
    return exp(-calc_sigma(pixel_coord, conic, xy));
}

// Splat pixel-space half-extent along each axis from a packed conic.
// Shared by project_forward and map_gaussians_to_intersects so both
// dispatches compute the same bbox.
fn compute_bbox_extent(conic: vec3f, power_threshold: f32) -> vec2f {
    let det = conic.x * conic.z - conic.y * conic.y;
    if det <= 0.0 {
        return vec2f(-1.0);
    }
    let inv_det = 1.0 / det;
    return vec2f(
        sqrt(2.0f * power_threshold * conic.z * inv_det),
        sqrt(2.0f * power_threshold * conic.x * inv_det),
    );
}

fn tile_rect(tile: vec2u) -> vec4f {
    let rect_min = vec2f(tile * TILE_WIDTH);
    let rect_max = rect_min + f32(TILE_WIDTH);
    return vec4f(rect_min.x, rect_min.y, rect_max.x, rect_max.y);
}

// Based on method from StopThePop: https://arxiv.org/pdf/2402.00525.
fn will_primitive_contribute(rect: vec4f, mean: vec2f, conic: vec3f, power_threshold: f32) -> bool {
    let x_left = mean.x < rect.x;
    let x_right = mean.x > rect.z;
    let in_x_range = !(x_left || x_right);

    let y_above = mean.y < rect.y;
    let y_below = mean.y > rect.w;
    let in_y_range = !(y_above || y_below);

    if (in_x_range && in_y_range) {
        return true;
    }

    let closest_corner = vec2f(
        select(rect.z, rect.x, x_left),
        select(rect.w, rect.y, y_above)
    );

    let width = rect.z - rect.x;
    let height = rect.w - rect.y;
    let d = vec2f(
        select(-f32(width), f32(width), x_left),
        select(-f32(height), f32(height), y_above)
    );

    let diff = mean - closest_corner;
    let t_max = vec2f(
        select(clamp((d.x * conic.x * diff.x + d.x * conic.y * diff.y) / (d.x * conic.x * d.x), 0.0f, 1.0f), 0.0f, in_y_range),
        select(clamp((d.y * conic.y * diff.x + d.y * conic.z * diff.y) / (d.y * conic.z * d.y), 0.0f, 1.0f), 0.0f, in_x_range)
    );

    let max_contribution_point = closest_corner + t_max * d;
    let max_power_in_tile = calc_sigma(mean, conic, max_contribution_point);

    return max_power_in_tile <= power_threshold;
}

fn ceil_div(a: u32, b: u32) -> u32 {
    return (a + b - 1) / b;
}

fn as_vec(packed: PackedVec3) -> vec3f {
    return vec3f(packed.x, packed.y, packed.z);
}

fn as_packed(vec: vec3f) -> PackedVec3 {
    return PackedVec3(vec.x, vec.y, vec.z);
}
